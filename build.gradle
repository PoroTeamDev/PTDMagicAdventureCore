plugins {
    id "idea"
    id "maven-publish"
    id 'net.minecraftforge.gradle' version '[6.0,6.2)'
    id 'com.diffplug.spotless' version '7.0.2'
    id 'org.spongepowered.mixin' version '0.7+'
}

version = mod_version
group = mod_group_id

base {
    archivesName = mod_id
}

// Mojang ships Java 17 to end users in 1.18+, so your mod should target Java 17.
java.toolchain.languageVersion = JavaLanguageVersion.of(17)

println "Java: ${System.getProperty 'java.version'}, JVM: ${System.getProperty 'java.vm.version'} (${System.getProperty 'java.vendor'}), Arch: ${System.getProperty 'os.arch'}"
def githubPackagesUser = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
def githubPackagesToken = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")

minecraft {

    mappings channel: mapping_channel, version: mapping_version

    copyIdeResources = true

    runs {

        configureEach {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'

            property 'forge.logging.console.level', 'debug'

            mods {
                "${mod_id}" {
                    source sourceSets.main
                }
            }
        }

        client {

            property 'forge.enabledGameTestNamespaces', mod_id
        }

        server {
            property 'forge.enabledGameTestNamespaces', mod_id
            args '--nogui'
        }

        gameTestServer {
            property 'forge.enabledGameTestNamespaces', mod_id
        }

        data {
            workingDirectory project.file('run-data')
            args '--mod', mod_id, '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    maven { // Registrate
        url = "https://maven.tterrag.com/"
        content {
            includeGroup("com.tterrag.registrate")
        }
    }

    maven {
        // Patchouli, JEI
        name = "BlameJared"
        url = "https://maven.blamejared.com/"
    }
    maven {
        url = "https://maven.theillusivec4.top/"
    }
    maven {
        // Curse Forge File
        url "https://cursemaven.com/"
        content {
            includeGroup "curse.maven"
        }
    }
    maven {
        // EMI
        name = "TerraformersMC"
        url = "https://maven.terraformersmc.com/"
    }

    maven {
        url = "https://maven.architectury.dev"
    }
    maven {
        url = "https://thedarkcolour.github.io/KotlinForForge/"
    }

    maven {
        //KJS
        url "https://maven.latvian.dev/releases"
        content {
            includeGroup "dev.latvian.mods"
            includeGroup "dev.latvian.apps"
        }
    }

    maven {
        url = "https://api.modrinth.com/maven"
        content {
            includeGroup "maven.modrinth"
        }
    }

    if (githubPackagesUser && githubPackagesToken) {
        maven {
            url "https://maven.pkg.github.com/CyclopsMC/packages"
            credentials {
                username = githubPackagesUser
                password = githubPackagesToken
            }
        }
    }
}

dependencies {

    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

    compileOnly fg.deobf("mezz.jei:jei-${minecraft_version}-forge-api:${jei_version}")
    compileOnly fg.deobf("mezz.jei:jei-${minecraft_version}-common-api:${jei_version}")
    runtimeOnly fg.deobf("mezz.jei:jei-${minecraft_version}-forge:${jei_version}")
    //runtimeOnly fg.deobf("dev.emi:emi-forge:${emi_version}+${minecraft_version}")
    runtimeOnly fg.deobf("dev.architectury:architectury-forge:9.2.14")
    //runtimeOnly fg.deobf("curse.maven:jade-324717:5390389")
    runtimeOnly fg.deobf("curse.maven:blood-magic-224791:7253333")
    runtimeOnly fg.deobf("curse.maven:botania-225643:6870713")
    runtimeOnly fg.deobf("maven.modrinth:divinerpg:iPO1zRlr")
    runtimeOnly fg.deobf("curse.maven:evilcraft-74610:7223463")
    runtimeOnly fg.deobf("maven.modrinth:l_enders-cataclysm:NvvXful4")
    runtimeOnly fg.deobf("curse.maven:cyclops-core-232758:7210370")
    runtimeOnly fg.deobf("curse.maven:patchouli-306770:6164575")
    runtimeOnly fg.deobf("curse.maven:curios-309927:6418456")
    runtimeOnly fg.deobf("curse.maven:lionfish-api-1001614:5922047")


    if (githubPackagesUser && githubPackagesToken) {
        runtimeOnly fg.deobf("org.cyclops.evilcraftcompat:EvilCraftCompat:1.1.0")
    }
    compileOnly fg.deobf("curse.maven:blood-magic-224791:7253333")
    compileOnly fg.deobf("curse.maven:botania-225643:6870713")
    compileOnly fg.deobf("maven.modrinth:divinerpg:iPO1zRlr")
    compileOnly fg.deobf("curse.maven:evilcraft-74610:7223463")
    compileOnly fg.deobf("maven.modrinth:l_enders-cataclysm:NvvXful4")

    implementation fg.deobf("dev.latvian.mods:kubejs-forge:${kubejs_version}")
    implementation fg.deobf("dev.latvian.mods:rhino-forge:${rhino_version}")
    //implementation fg.deobf("com.lowdragmc.ldlib:ldlib-forge-${minecraft_version}:${ldlib_version}") { transitive = false }
    implementation fg.deobf("com.tterrag.registrate:Registrate:${registrate_version}")


}


tasks.named('processResources', ProcessResources).configure {
    var replaceProperties = [
            minecraft_version: minecraft_version, minecraft_version_range: minecraft_version_range,
            forge_version: forge_version, forge_version_range: forge_version_range,
            loader_version_range: loader_version_range,
            mod_id: mod_id, mod_name: mod_name, mod_license: mod_license, mod_version: mod_version,
            mod_authors: mod_authors, mod_description: mod_description,
    ]
    inputs.properties replaceProperties

    filesMatching(['META-INF/mods.toml', 'pack.mcmeta']) {
        expand replaceProperties + [project: project]
    }
}


tasks.named('jar', Jar).configure {
    manifest {
        attributes([
                'Specification-Title'     : mod_id,
                'Specification-Vendor'    : mod_authors,
                'Specification-Version'   : '1', // We are version 1 of ourselves
                'Implementation-Title'    : project.name,
                'Implementation-Version'  : project.jar.archiveVersion,
                'Implementation-Vendor'   : mod_authors,
                'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }

    // This is the preferred method to reobfuscate your jar file
    finalizedBy 'reobfJar'
}

publishing {
    publications {
        register('mavenJava', MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/mcmodsrepo"
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}
